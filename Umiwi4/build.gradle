apply plugin: 'com.android.application'

apply plugin: 'bugly'   //添加Bugly符号表插件

bugly {
    appId = '900012791'    //注册时分配的App ID
    appKey = 'zyOJ2I2aOT38zaVs'  //注册时分配的App Key
}

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')
    compile 'com.orhanobut:dialogplus:1.7@aar'
    compile 'com.ogaclejapan.smarttablayout:library:1.1.3@aar'
    compile 'com.ogaclejapan.smarttablayout:utils-v4:1.1.3@aar'
    compile 'com.umeng.analytics:analytics:latest.integration'
    //    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    //    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
    //    compile 'com.squareup:otto:1.3.8'
    compile project(':YoumiLogin')
    compile project(':YoumiResource')
    compile project(':YoumiPay')
    compile project(':YoumiShare')
    compile project(':YouYouShuoCamera')
    //    compile project(':ijkplayer-exo')
    compile project(':ijkplayer-java')
    compile 'com.tencent.bugly:crashreport:latest.release'
    //其中latest.release为当前最新的Bugly SDK版本号
    compile 'com.android.support:design:23.1.0'
    compile 'com.jakewharton:butterknife:5.1.1'
    compile files('libs/nineoldandroids-2.4.0.jar')
}

android {

    compileSdkVersion 24
    buildToolsVersion '24.0.2'

    useLibrary 'org.apache.http.legacy' // 兼容 apache api

    defaultConfig {//指令集 是不是用armeabi-v7a
        applicationId PACKAGE_NAME
        minSdkVersion 14
        targetSdkVersion 23
        versionCode Integer.parseInt(VERSION_CODE)
        versionName VERSION_NAME

        ndkConfig.abiFilters "armeabi-v7a"
        ndkConfig.abiFilters "armeabi"
        ndkConfig.abiFilters "arm64-v8a"
        ndkConfig.abiFilters "x86"
        ndkConfig.abiFilters "x86_64"

        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "itest"]
    }



    productFlavors {  //./gradlew assembleRelease
       // itest {}
        portalv {}//官网
        baiduvt {}
        smvpay {}//神马
        googleplayv {}
        huaweiv {}
        lenovov {}
        m91v {}
        m360vt {}
        meizuv {}
        qqv {}
        qqvpay {} //付费包
        sogou {}
        taobaovt {}
        wandoujiav {}
        xiaomivt {}
        anzhiv {}//安智
        domob {}//多盟
        youmiad1 {}//有米
        adsage {}//艾德思奇
      //  all32 {}
       // all64 { minSdkVersion 21 }
    }

    signingConfigs {
        //debug版本不签名
        debug {

        }
        //release版本签名信息
        release {
            try {
                storeFile file("./android.keystore")
                storePassword KEYSTORE_PASSWORD
                keyAlias KEY_ALIAS
                keyPassword KEY_PASSWORD
            } catch (ex) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.");
            }
        }

    }

    buildTypes {
        //debug版本编译是配置
        debug {
            // 显示Log
            buildConfigField "boolean", "LOG_DEBUG", "true"

            debuggable true
            versionNameSuffix "-debug"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            signingConfig signingConfigs.debug
        }

        //release版本编译时配置
        release {
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"

            debuggable false
            minifyEnabled true
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            // 混淆 拆分处理
            //More info: https://github.com/krschultz/android-proguard-snippets
            proguardFile '../settings/proguard/proguard-support-v7-appcompat.pro'
            proguardFile '../settings/proguard/proguard-alipay.pro'
            proguardFile '../settings/proguard/proguard-ijkplayer.pro'
            proguardFile '../settings/proguard/proguard-baidu.pro'
            proguardFile '../settings/proguard/proguard-gson.pro'
            proguardFile '../settings/proguard/proguard-sharesdk.pro'
            proguardFile '../settings/proguard/proguard-three-github.pro'
            proguardFile '../settings/proguard/proguard-weixin.pro'
            proguardFile '../settings/proguard/proguard-qq.pro'
            proguardFile '../settings/proguard/proguard-youmi-account.pro'
            proguardFile '../settings/proguard/proguard-youmi-framework.pro'
            proguardFile '../settings/proguard/proguard-youmi-pay.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为test_v1.0_2015-01-15_wandoujia.apk
                        def fileName = "${APP_NAME}_v${defaultConfig.versionName}_${releaseTime()}_${variant.productFlavors[0].name}.apk"
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
    android {
        lintOptions {
            checkReleaseBuilds false
            // Or, if you prefer, you can continue to check for errors in release builds,
            // but continue the build even when errors are found:
            abortOnError false
        }
    }

    sourceSets {
        main {
            jni.srcDirs = []
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            resources.srcDirs = ['resources']
            res.srcDirs = ['src/main/res']
            aidl.srcDirs = ['src/main/aidl']
            renderscript.srcDirs = ['src/main/rs']
            assets.srcDirs = ['src/main/assets']
            jniLibs.srcDirs = ['src/main/jniLibs']
        }

        androidTest.setRoot('tests')
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

}

apply plugin: 'idea'

idea {
    module {
        downloadJavadoc = true // 下载依赖项目的文档
        downloadSources = true // 下载依赖项目的源码
    }
}